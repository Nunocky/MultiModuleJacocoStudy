plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 27
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    // sharedTestでもっと便利に
    // > testでもandroidTestでも実行可能なsharedTest
    // https://qiita.com/keidroid/items/adc4f065b84d8a2cd17a#optional-sharedtest%E3%81%A7%E3%82%82%E3%81%A3%E3%81%A8%E4%BE%BF%E5%88%A9%E3%81%AB
    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    jacoco {
        version = rootProject.ext.jacoco_version
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }

        unitTests.all {
            // IntelliJ Code Coverage + Robolectricの注意点
            // https://qiita.com/keidroid/items/adc4f065b84d8a2cd17a#intellij-code-coverage--robolectric%E3%81%AE%E6%B3%A8%E6%84%8F%E7%82%B9
            jvmArgs '-noverify'
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.test.ext:junit-ktx:1.1.3'

    testImplementation 'junit:junit:4.+'

    // Robolectric
    testImplementation "org.robolectric:robolectric:4.7.3"

    // AndroidX Testing Library (test)
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'androidx.test:rules:1.4.0'
    testImplementation 'androidx.test:runner:1.4.0'
    testImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation 'androidx.test.ext:truth:1.4.0'

    // AndroidX Testing Library (androidTest)
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.ext:truth:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

apply from: '../jacoco.gradle'
